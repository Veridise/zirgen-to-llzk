// RUN: zklang -o - %s --print-debug-info=false | FileCheck %s --enable-var-scope

component Foo(x: ExtVal) {
  ExtInv(x)


// CHECK-LABEL: llzk.struct @Foo<[]> {
// CHECK: field @"$super" : !llzk.array<4 x !llzk.felt>

// CHECK-LABEL: func @compute
// CHECK-SAME: (%[[A:[0-9a-zA-Z_\.]+]]: !llzk.array<4 x !llzk.felt>) -> !llzk.struct<@Foo<[]>> {

// CHECK: %[[BETA:[0-9a-zA-Z_\.]+]] = constfelt  11
// CHECK: %[[C3:[0-9a-zA-Z_\.]+]] = arith.constant  3
// CHECK: %[[C2:[0-9a-zA-Z_\.]+]] = arith.constant  2
// CHECK: %[[C1:[0-9a-zA-Z_\.]+]] = arith.constant  1
// CHECK: %[[C0:[0-9a-zA-Z_\.]+]] = arith.constant  0
// CHECK: %[[A0:[0-9a-zA-Z_\.]+]] = readarr %[[A]][%[[C0]]] : <4 x !llzk.felt>, !llzk.felt
// CHECK: %[[A1:[0-9a-zA-Z_\.]+]] = readarr %[[A]][%[[C1]]] : <4 x !llzk.felt>, !llzk.felt
// CHECK: %[[A2:[0-9a-zA-Z_\.]+]] = readarr %[[A]][%[[C2]]] : <4 x !llzk.felt>, !llzk.felt
// CHECK: %[[A3:[0-9a-zA-Z_\.]+]] = readarr %[[A]][%[[C3]]] : <4 x !llzk.felt>, !llzk.felt

// COM:                                                   a[2] * a[2]
// CHECK: %[[T2:[0-9a-zA-Z_\.]+]] = mul %[[A2]], %[[A2]]
// COM:                                    a[3] + a[3]
// CHECK: %[[T1:[0-9a-zA-Z_\.]+]] = add %[[A3]], %[[A3]]
// COM:                            a[1] * (a[3] + a[3])
// CHECK: %[[T3:[0-9a-zA-Z_\.]+]] = mul %[[A1]], %[[T1]]
// COM:                            a[1] * (a[3] + a[3]) - a[2] * a[2]
// CHECK: %[[T4:[0-9a-zA-Z_\.]+]] = sub %[[T3]], %[[T2]]
// COM:                    BETA * (a[1] * (a[3] + a[3]) - a[2] * a[2])
// CHECK: %[[T5:[0-9a-zA-Z_\.]+]] = mul %[[T4]], %[[BETA]]
// COM:      a[0] * a[0]
// CHECK: %[[T0:[0-9a-zA-Z_\.]+]] = mul %[[A0]], %[[A0]]
// COM: b0 = a[0] * a[0] + BETA * (a[1] * (a[3] + a[3]) - a[2] * a[2])
// CHECK: %[[B0:[0-9a-zA-Z_\.]+]] = add %[[T0]], %[[T5]]

// COM:                                                   a[3] * a[3]
// CHECK: %[[T9:[0-9a-zA-Z_\.]+]] = mul %[[A3]], %[[A3]]
// COM:                                           BETA * (a[3] * a[3])
// CHECK: %[[T10:[0-9a-zA-Z_\.]+]] = mul %[[T9]], %[[BETA]]
// COM:                             a[1] * a[1]
// CHECK: %[[T8:[0-9a-zA-Z_\.]+]] = mul %[[A1]], %[[A1]]
// COM:              a[2] + a[2]
// CHECK: %[[T6:[0-9a-zA-Z_\.]+]] = add %[[A2]], %[[A2]]
// COM:      a[0] * (a[2] + a[2])
// CHECK: %[[T7:[0-9a-zA-Z_\.]+]] = mul %[[A0]], %[[T6]]
// COM:      a[0] * (a[2] + a[2]) - a[1] * a[1]
// CHECK: %[[T11:[0-9a-zA-Z_\.]+]] = sub %[[T7]], %[[T8]]
// COM: b2 = a[0] * (a[2] + a[2]) - a[1] * a[1] + BETA * (a[3] * a[3])
// CHECK: %[[B2:[0-9a-zA-Z_\.]+]] = add %[[T11]], %[[T10]]

// COM:               BETA * b2
// CHECK: %[[T13:[0-9a-zA-Z_\.]+]] = mul %[[B2]], %[[BETA]]
// COM:               BETA * b2 * b2
// CHECK: %[[T14:[0-9a-zA-Z_\.]+]] = mul %[[T13]], %[[B2]]
// COM:     b0 * b0
// CHECK: %[[T12:[0-9a-zA-Z_\.]+]] = mul %[[B0]], %[[B0]]
// COM: c = b0 * b0 + BETA * b2 * b2
// CHECK: %[[C:[0-9a-zA-Z_\.]+]] = add %[[T12]], %[[T14]]

// COM: ic = c.inv()
// CHECK: %[[IC:[0-9a-zA-Z_\.]+]] = inv %[[C]]

// COM: b0' = b0 * ic
// CHECK: %[[B0bis:[0-9a-zA-Z_\.]+]] = mul %[[B0]], %[[IC]]

// COM: b2' = b2 * ic
// CHECK: %[[B2bis:[0-9a-zA-Z_\.]+]] = mul %[[B2]], %[[IC]]

// COM:                      BETA * a[2]
// CHECK: %[[T16:[0-9a-zA-Z_\.]+]] = mul %[[A2]], %[[BETA]]
// COM:                      BETA * a[2] * b2'
// CHECK: %[[T17:[0-9a-zA-Z_\.]+]] = mul %[[T16]], %[[B2bis]]
// COM:         a[0] * b0'
// CHECK: %[[T15:[0-9a-zA-Z_\.]+]] = mul %[[A0]], %[[B0bis]]
// COM: out_0 = a[0] * b0' + BETA * a[2] * b2'
// CHECK: %[[OUT0:[0-9a-zA-Z_\.]+]] = add %[[T15]], %[[T17]]

// COM:                       BETA * a[3]
// CHECK: %[[T20:[0-9a-zA-Z_\.]+]] = mul %[[A3]], %[[BETA]]
// COM:                       BETA * a[3] * b2'
// CHECK: %[[T21:[0-9a-zA-Z_\.]+]] = mul %[[T20]], %[[B2bis]]
// COM:         -a[1]
// CHECK: %[[T18:[0-9a-zA-Z_\.]+]] = neg %[[A1]]
// COM:         -a[1] * b0'
// CHECK: %[[T19:[0-9a-zA-Z_\.]+]] = mul %[[T18]], %[[B0bis]]
// COM: out_1 = -a[1] * b0' - BETA * a[3] * b2'
// CHECK: %[[OUT1:[0-9a-zA-Z_\.]+]] = sub %[[T19]], %[[T21]]

// COM:                       a[2] * b0'
// CHECK: %[[T24:[0-9a-zA-Z_\.]+]] = mul %[[A2]], %[[B0bis]]
// COM:         -a[0]
// CHECK: %[[T22:[0-9a-zA-Z_\.]+]] = neg %[[A0]]
// COM:         -a[0] * b2'
// CHECK: %[[T23:[0-9a-zA-Z_\.]+]] = mul %[[T22]], %[[B2bis]]
// COM: out_2 = -a[0] * b2' + a[2] * b0'
// CHECK: %[[OUT2:[0-9a-zA-Z_\.]+]] = add %[[T23]], %[[T24]]

// COM:                      a[3] * b0'
// CHECK: %[[T26:[0-9a-zA-Z_\.]+]] = mul %[[A3]], %[[B0bis]]
// COM:         a[1] * b2'
// CHECK: %[[T25:[0-9a-zA-Z_\.]+]] = mul %[[A1]], %[[B2bis]]
// COM: out_3 = a[1] * b2' - a[3] * b0'
// CHECK: %[[OUT3:[0-9a-zA-Z_\.]+]] = sub %[[T25]], %[[T26]]

// CHECK: %[[T27:[0-9a-zA-Z_\.]+]] = new_array %[[OUT0]], %[[OUT1]], %[[OUT2]], %[[OUT3]] : <4 x !llzk.felt>

// CHECK-LABEL: func @constrain(%arg0: !llzk.struct<@Foo<[]>>, 


}
